;; reward-token.clar
;; SIP-010 compliant reward token for yield scheduler

;; Commented out for Clarity 4 local testing
;; (impl-trait 'SP3FBR2AGK5H9QBDH3EEN6DF8EK8JY7RX8QJ5SVTE.sip-010-trait-ft-standard.sip-010-trait)

(define-constant CONTRACT_OWNER tx-sender)
(define-constant ERR_NOT_AUTHORIZED (err u100))
(define-constant ERR_INSUFFICIENT_BALANCE (err u101))

(define-fungible-token reward-token)

(define-data-var token-name (string-ascii 32) "Yield Reward Token")
(define-data-var token-symbol (string-ascii 10) "YRT")
(define-data-var token-decimals uint u6)
(define-data-var token-uri (optional (string-utf8 256)) none)

(define-map allowed-minters principal bool)

(define-read-only (get-name)
    (ok (var-get token-name)))

(define-read-only (get-symbol)
    (ok (var-get token-symbol)))

(define-read-only (get-decimals)
    (ok (var-get token-decimals)))

(define-read-only (get-balance (account principal))
    (ok (ft-get-balance reward-token account)))

(define-read-only (get-total-supply)
    (ok (ft-get-supply reward-token)))

(define-read-only (get-token-uri)
    (ok (var-get token-uri)))

(define-public (transfer (amount uint) (sender principal) (recipient principal) (memo (optional (buff 34))))
    (begin
        (asserts! (is-eq tx-sender sender) ERR_NOT_AUTHORIZED)
        (try! (ft-transfer? reward-token amount sender recipient))
        (match memo to-print (print to-print) 0x)
        (ok true)))

(define-public (mint (amount uint) (recipient principal))
    (begin
        (asserts! (or (is-eq tx-sender CONTRACT_OWNER)
                     (default-to false (map-get? allowed-minters tx-sender))) ERR_NOT_AUTHORIZED)
        (ft-mint? reward-token amount recipient)))

(define-public (burn (amount uint))
    (ft-burn? reward-token amount tx-sender))

(define-public (set-minter (minter principal) (allowed bool))
    (begin
        (asserts! (is-eq tx-sender CONTRACT_OWNER) ERR_NOT_AUTHORIZED)
        (map-set allowed-minters minter allowed)
        (ok true)))

(define-public (set-token-uri (new-uri (optional (string-utf8 256))))
    (begin
        (asserts! (is-eq tx-sender CONTRACT_OWNER) ERR_NOT_AUTHORIZED)
        (var-set token-uri new-uri)
        (ok true)))
